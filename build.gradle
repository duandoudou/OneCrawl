println("******************")

// project的额外属性，这里用于定义profile属性，模拟maven的profile
ext {
    // profile 默认值 在 gradle.properties 中定义
    println "profile is : ${profile}"
}
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}


apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'

jar {
    baseName = 'one-crawl'
    version = '0.1.0'
}

repositories {
    mavenLocal()
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {

    // Spring Boot 对这些运维相关的功能进行了整合，形成了一个功能完备和可定制的功能集，称之为 Actuator。
    compile("org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}")

    compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-freemarker:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-remote-shell:${springBootVersion}")

    if (profile != "stable") {
        compile("org.springframework.boot:spring-boot-devtools:${springBootVersion}")
        println "compile devtool"
    } else {
        println "not compile actuator"
    }


    compile('org.apache.commons:commons-pool2:2.4.2')
    compile("org.codehaus.groovy:groovy-all:2.4.6")

    compile('us.codecraft:webmagic-core:0.5.3'){
        force = true
        exclude group: 'org.slf4j', module: 'slf4j-log4j12' //by both name and group
        exclude group:'com.jayway.jsonpath',module:'json-path'
    }
    compile('us.codecraft:webmagic-extension:0.5.3'){
        exclude group: 'org.slf4j', module: 'slf4j-log4j12' //by both name and group
    }
    compile('com.jayway.jsonpath:json-path:0.8.1'){
        exclude group: 'org.slf4j', module: 'slf4j-api' //by both name and group
    }
    compile('com.google.guava:guava:18.0')
    compile('org.apache.thrift:libthrift:0.9.3')
    compile('commons-validator:commons-validator:1.4.0')
    compile('redis.clients:jedis:2.8.0')

    testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")

}

compileJava.dependsOn(processResources)

sourceSets {
    main {
        resources {
            srcDir "conf"
            srcDir 'cmd'
            srcDir 'script'
        }
    }
}
idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/main/")
    }
}

task wrapper(type: Wrapper) {
    def version = '2.10'
    gradleVersion = version
    distributionUrl = "http://7xnyyx.com1.z0.glb.clouddn.com/gradle-${version}-all.zip"
}


mainClassName="com.seveniu.AppCrawl"
bootRun {
    systemProperties.putAt("spring.profiles.active",profile)
    addResources = true
}

task helloworld << {
    println("hello world")
}

println("====================")
